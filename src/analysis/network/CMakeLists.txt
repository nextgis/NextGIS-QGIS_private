

#############################################################
# sources

SET(QGIS_NETWORK_ANALYSIS_SRCS
  qgsgraph.cpp
  qgsgraphbuilder.cpp
  qgsdistancearcproperter.cpp
  qgslinevectorlayerdirector.cpp
  qgsgraphanalyzer.cpp
)

INCLUDE_DIRECTORIES(BEFORE raster)

SET(QGIS_NETWORK_ANALYSIS_MOC_HDRS
  qgsgraphdirector.h
  qgslinevectorlayerdirector.h
)

QT4_WRAP_CPP(QGIS_NETWORK_ANALYSIS_MOC_SRCS ${QGIS_NETWORK_ANALYSIS_MOC_HDRS})

# install headers

SET(QGIS_NETWORK_ANALYSIS_HDRS
  qgsgraph.h
  qgsgraphbuilderintr.h
  qgsgraphbuilder.h
  qgsarcproperter.h
  qgsdistancearcproperter.h
  qgsgraphdirector.h
  qgslinevectorlayerdirector.h
  qgsgraphanalyzer.h
)

INCLUDE_DIRECTORIES(
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_SOURCE_DIR}../../core/
)
INCLUDE_DIRECTORIES(SYSTEM
  ${PROJ_INCLUDE_DIR}
  ${GEOS_INCLUDE_DIR}
  ${GDAL_INCLUDE_DIR}
)

#############################################################
# qgis_analysis library

ADD_LIBRARY(${QGIS_NETWORKANALYSIS_TARGET_NAME} SHARED ${QGIS_NETWORK_ANALYSIS_SRCS} ${QGIS_NETWORK_ANALYSIS_MOC_SRCS} ${QGIS_NETWORK_ANALYSIS_HDRS})

IF(MSVC)
  SET_TARGET_PROPERTIES(${QGIS_NETWORKANALYSIS_TARGET_NAME} PROPERTIES LINK_FLAGS "/FORCE:MULTIPLE")
ENDIF(MSVC)

IF(NOT APPLE)
  INSTALL(FILES ${QGIS_NETWORK_ANALYSIS_HDRS} ${QGIS_NETWORK_ANALYSIS_MOC_HDRS} DESTINATION ${QGIS_INCLUDE_DIR})
ELSE(NOT APPLE)
  SET_TARGET_PROPERTIES(${QGIS_NETWORKANALYSIS_TARGET_NAME} PROPERTIES
    CLEAN_DIRECT_OUTPUT 1
    FRAMEWORK 1
    FRAMEWORK_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}"
    MACOSX_FRAMEWORK_INFO_PLIST "${CMAKE_SOURCE_DIR}/mac/framework.info.plist.in"
    MACOSX_FRAMEWORK_SHORT_VERSION_STRING ${${NGQ_VERSION}}
    MACOSX_FRAMEWORK_IDENTIFIER org.qgis.qgis2_networkanalysis
    BUILD_WITH_INSTALL_RPATH TRUE
    PUBLIC_HEADER "${QGIS_NETWORK_ANALYSIS_HDRS};${QGIS_NETWORK_ANALYSIS_MOC_HDRS}"
    LINK_FLAGS "${CMAKE_SHARED_LINKER_FLAGS}"
  )
ENDIF(NOT APPLE)

#generate unversioned libs for android
IF (NOT ANDROID)
  SET_TARGET_PROPERTIES(${QGIS_NETWORKANALYSIS_TARGET_NAME} PROPERTIES
    VERSION ${NGQ_VERSION}
    SOVERSION ${NGQ_VERSION}
    )
ENDIF (NOT ANDROID)

ADD_DEPENDENCIES(${QGIS_NETWORKANALYSIS_TARGET_NAME} ${QGIS_CORE_TARGET_NAME})

TARGET_LINK_LIBRARIES(${QGIS_NETWORKANALYSIS_TARGET_NAME} ${QGIS_CORE_TARGET_NAME})

# install

INSTALL(TARGETS ${QGIS_NETWORKANALYSIS_TARGET_NAME}
  RUNTIME DESTINATION ${QGIS_BIN_DIR}
  LIBRARY DESTINATION ${QGIS_LIB_DIR}
  ARCHIVE DESTINATION ${QGIS_LIB_DIR}
  FRAMEWORK DESTINATION ${QGIS_FW_SUBDIR}
  PUBLIC_HEADER DESTINATION ${QGIS_INCLUDE_DIR})

# Mac dev frameworks

IF (APPLE AND QGIS_MACAPP_INSTALL_DEV)
  INSTALL(TARGETS ${QGIS_NETWORKANALYSIS_TARGET_NAME} FRAMEWORK DESTINATION ${QGIS_MACAPP_DEV_PREFIX})
  INSTALL(CODE "EXECUTE_PROCESS(COMMAND install_name_tool -id \"${QGIS_MACAPP_DEV_PREFIX}/${QGIS_NETWORKANALYSIS_TARGET_NAME}.framework/Versions/${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}/${QGIS_NETWORKANALYSIS_TARGET_NAME}\" \"$ENV{DESTDIR}${QGIS_MACAPP_DEV_PREFIX}/${QGIS_NETWORKANALYSIS_TARGET_NAME}.framework/${QGIS_NETWORKANALYSIS_TARGET_NAME}\")")
  INSTALL(CODE "EXECUTE_PROCESS(COMMAND install_name_tool -change \"${CMAKE_INSTALL_NAME_DIR}/${QGIS_CORE_TARGET_NAME}.framework/Versions/${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}/${QGIS_CORE_TARGET_NAME}\" \"${QGIS_MACAPP_DEV_PREFIX}/${QGIS_CORE_TARGET_NAME}.framework/Versions/${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}/${QGIS_CORE_TARGET_NAME}\" \"$ENV{DESTDIR}${QGIS_MACAPP_DEV_PREFIX}/${QGIS_NETWORKANALYSIS_TARGET_NAME}.framework/${QGIS_NETWORKANALYSIS_TARGET_NAME}\")")
ENDIF (APPLE AND QGIS_MACAPP_INSTALL_DEV)
